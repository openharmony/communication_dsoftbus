# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("../../../../../../../../dsoftbus.gni")

module_output_path = "dsoftbus/transmission"
dsoftbus_root_path = "../../../../../../../.."

## UnitTest VtpInstanceTest {{{
ohos_unittest("VtpInstanceTest") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  module_out_path = module_output_path
  sources = [ "vtp_instance_test.cpp" ]

  include_dirs = [
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_root_path/core/frame/common/include",
    "$dsoftbus_root_path/core/transmission/common/include",
    "$dsoftbus_root_path/sdk/transmission/session/include",
    "$dsoftbus_root_path/sdk/transmission/trans_channel/udp/stream/libsoftbus_stream",
    "$dsoftbus_root_path/sdk/transmission/trans_channel/udp/stream/libsoftbus_stream/include",
    "$dsoftbus_root_path/adapter/common/include",
    "$dsoftbus_root_path/components/nstackx/fillp/include",
    "$dsoftbus_root_path/sdk/transmission/trans_channel/udp/stream/include",
    "$dsoftbus_root_path/sdk/transmission/trans_channel/udp/common/include",
  ]

  deps = [
    "$dsoftbus_root_path/core/common:softbus_utils",
    "$dsoftbus_root_path/core/frame:softbus_server",
    "$dsoftbus_root_path/tests/sdk:softbus_client_static",
  ]

  native_source_path = rebase_path("$dsoftbus_root_path")
  dep_file = "components/nstackx_enhanced/nstackx_core/dfile/BUILD.gn"
  enhanced = exec_script("$dsoftbus_root_path/check_sub_module.py",
                         [
                           "$native_source_path",
                           "$dep_file",
                         ],
                         "value")
  if (enhanced) {
    deps += [
      "$dsoftbus_root_path/components/nstackx_enhanced/fillp:FillpSo",
      "$dsoftbus_root_path/components/nstackx_enhanced/nstackx_core/dfile:nstackx_dfile",
    ]
  } else {
    deps += [ "$dsoftbus_root_path/components/nstackx/fillp:FillpSo.open" ]
  }

  external_deps = [
    "bounds_checking_function:libsec_shared",
    "c_utils:utils",
    "hilog:libhilog",
  ]
}

group("unittest") {
  testonly = true
  deps = []
  deps += [
    # deps file
    ":VtpInstanceTest",
  ]
}
