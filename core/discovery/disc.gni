# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../core/discovery/ble/disc_ble.gni")
import("../../core/discovery/coap/disc_coap.gni")
import("../../core/discovery/event_manager/disc_event_manager.gni")
import("../../dsoftbus.gni")

disc_server_src = ble_discovery_src + disc_coap_src + disc_event_manager_src
disc_server_inc = ble_discovery_inc + disc_coap_inc + disc_event_manager_inc
disc_server_deps = ble_discovery_deps + disc_coap_deps + disc_event_manager_deps
disc_server_external_deps = []
disc_server_src += [
  "$dsoftbus_root_path/core/discovery/manager/src/disc_manager.c",
  "$dsoftbus_root_path/core/discovery/manager/src/softbus_disc_server.c",
]
disc_server_inc += [
  "$dsoftbus_root_path/core/discovery/ipc/include",
  "$dsoftbus_root_path/core/discovery/ipc/include",
  "$dsoftbus_root_path/core/discovery/manager/include",
  "$dsoftbus_root_path/core/discovery/interface",
  "$dsoftbus_root_path/core/common/dfx/hisysevent_adapter/include",
]
if (defined(ohos_lite)) {
  if (ohos_kernel_type == "liteos_m") {
    disc_server_src +=
        [ "$dsoftbus_root_path/core/discovery/ipc/mini/disc_client_proxy.c" ]
    disc_server_inc += [ "$dsoftbus_root_path/sdk/discovery/manager/include" ]
  } else {
    disc_server_src +=
        [ "$dsoftbus_root_path/core/discovery/ipc/small/disc_client_proxy.c" ]
    disc_server_deps += [
      "//foundation/communication/ipc/interfaces/innerkits/c/ipc:ipc_single",
    ]
  }
} else {
  disc_server_src += [
    "$dsoftbus_root_path/core/discovery/ipc/$os_type/src/disc_client_proxy.cpp",
    "$dsoftbus_root_path/core/discovery/ipc/$os_type/src/disc_client_proxy_standard.cpp",
  ]
  disc_server_inc += [
    "$dsoftbus_root_path/core/discovery/ipc/$os_type/include",
    "//utils/system/safwk/native/include",
  ]
  disc_server_external_deps += [ "c_utils:utils" ]
}
