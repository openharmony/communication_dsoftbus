# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

core_lane_hub_path = "$dsoftbus_root_path/core/bus_center/lnn/lane_hub"

native_source_path = rebase_path("$dsoftbus_root_path")
dep_dir = "dsoftbus_enhance/core/bus_center/lnn/lane_hub"
enhanced = exec_script("$dsoftbus_root_path/check_sub_module.py",
                       [
                         "$native_source_path",
                         "$dep_dir",
                       ],
                       "value")

bus_center_hub_inc = [
  "$core_lane_hub_path/common/include",
  "$core_lane_hub_path/lane_manager/include",
  "$core_lane_hub_path/time_sync/include",
  "$core_lane_hub_path/heartbeat/include/",
]
bus_center_hub_deps = []

if (dsoftbus_standard_feature_lnn_net) {
  bus_center_hub_src = [
    "$core_lane_hub_path/common/src/lnn_lane_hub.c",
    "$core_lane_hub_path/lane_manager/src/lnn_lane_info.c",
    "$core_lane_hub_path/lane_manager/src/lnn_lane_link.c",
    "$core_lane_hub_path/lane_manager/src/lnn_lane_manager.c",
    "$core_lane_hub_path/lane_manager/src/lnn_smart_communication.c",
  ]
  if (dsoftbus_standard_feature_lnn_time_sync) {
    bus_center_hub_src +=
        [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_manager.c" ]
    if (!enhanced) {
      bus_center_hub_src +=
          [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_impl_virtual.c" ]
    }
  } else {
    bus_center_hub_src +=
        [ "$core_lane_hub_path/time_sync/src/lnn_time_sync_manager_virtual.c" ]
  }
  if (dsoftbus_standard_feature_lnn_heartbeat) {
    bus_center_hub_inc +=
        [ "$dsoftbus_core_path/connection/p2p/common/include/p2plink_type.h" ]
    bus_center_hub_src += [
      "$core_lane_hub_path/heartbeat/src/lnn_heartbeat_strategy.c",
      "$core_lane_hub_path/heartbeat/src/lnn_heartbeat_manager.c",
      "$core_lane_hub_path/heartbeat/src/lnn_heartbeat_fsm.c",
    ]
    if (!enhanced) {
      bus_center_hub_src += [
        "$core_lane_hub_path/heartbeat/src/lnn_ble_heartbeat_impl_virtual.c",
      ]
    }
  } else {
    bus_center_hub_src +=
        [ "$core_lane_hub_path/heartbeat/src/lnn_heartbeat_strategy_virtual.c" ]
  }
} else {
  bus_center_hub_src = [
    "$core_lane_hub_path/common/src/lnn_lane_hub_virtual.c",
    "$core_lane_hub_path/time_sync/src/lnn_time_sync_manager_virtual.c",
    "$core_lane_hub_path/heartbeat/src/lnn_heartbeat_strategy_virtual.c",
  ]
}

if (dsoftbus_standard_feature_lnn_net &&
    dsoftbus_standard_feature_lnn_time_sync && enhanced) {
  bus_center_hub_deps += [ "$dsoftbus_root_path/dsoftbus_enhance/core/bus_center/lnn/lane_hub/time_sync:dsoftbus_time_sync_impl" ]
}
if (dsoftbus_standard_feature_lnn_net &&
    dsoftbus_standard_feature_lnn_heartbeat && enhanced) {
  bus_center_hub_deps += [ "$dsoftbus_root_path/dsoftbus_enhance/core/bus_center/lnn/lane_hub/heartbeat:dsoftbus_heartbeat_impl" ]
}
