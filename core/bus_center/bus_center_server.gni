# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import(
    "//foundation/communication/dsoftbus/adapter/common/bus_center/bus_center_adapter.gni")
import("//foundation/communication/dsoftbus/adapter/common/net/hdi/net_hdi.gni")
import(
    "//foundation/communication/dsoftbus/adapter/common/net/wifi/net_wifi.gni")
import("//foundation/communication/dsoftbus/core/adapter/core_adapter.gni")
import(
    "//foundation/communication/dsoftbus/core/bus_center/lnn/disc_mgr/bus_center_disc_mgr.gni")
import(
    "//foundation/communication/dsoftbus/core/bus_center/lnn/lane_hub/lane_hub.gni")
import(
    "//foundation/communication/dsoftbus/core/bus_center/lnn/net_builder/net_builder.gni")
import(
    "//foundation/communication/dsoftbus/core/bus_center/lnn/net_buscenter/net_manager.gni")
import(
    "//foundation/communication/dsoftbus/core/bus_center/lnn/net_ledger/net_ledger.gni")
import(
    "//foundation/communication/dsoftbus/core/bus_center/monitor/bus_center_monitor.gni")
import(
    "//foundation/communication/dsoftbus/core/bus_center/utils/bus_center_utils.gni")

bus_center_server_src =
    bus_center_adapter_src + bus_center_core_adapter_src +
    bus_center_disc_mgr_src + bus_center_hub_src + bus_center_builder_src +
    bus_center_net_mgr_src + bus_center_ledger_src + bus_center_monitor_src +
    bus_center_utils_src + adapter_wifi_src + adapter_hdi_src
bus_center_server_inc =
    bus_center_adapter_inc + bus_center_core_adapter_inc +
    bus_center_disc_mgr_inc + bus_center_hub_inc + bus_center_builder_inc +
    bus_center_net_mgr_inc + bus_center_ledger_inc + bus_center_monitor_inc +
    bus_center_utils_inc + adapter_wifi_inc + adapter_hdi_inc
bus_center_server_deps =
    bus_center_adapter_deps + bus_center_core_adapter_deps +
    bus_center_disc_mgr_deps + bus_center_hub_deps + bus_center_builder_deps +
    bus_center_net_mgr_deps + bus_center_ledger_deps + bus_center_monitor_deps +
    bus_center_utils_deps + adapter_wifi_deps + adapter_hdi_deps

bus_center_server_src +=
    [ "$dsoftbus_root_path/core/bus_center/service/src/bus_center_manager.c" ]
bus_center_server_src +=
    [ "$dsoftbus_root_path/core/bus_center/service/src/bus_center_event.c" ]

bus_center_server_inc += [
  "$dsoftbus_root_path/core/bus_center/interface",
  "$dsoftbus_root_path/core/bus_center/ipc/include",
  "$dsoftbus_root_path/core/bus_center/service/include",
  "$dsoftbus_root_path/sdk/bus_center/manager/include",
]

bus_center_server_external_deps = []

if (defined(ohos_lite)) {
  if (ohos_kernel_type == "liteos_m") {
    bus_center_server_src +=
        [ "$dsoftbus_root_path/core/bus_center/ipc/mini/lnn_bus_center_ipc.c" ]
  } else {
    bus_center_server_src += [
      "$dsoftbus_root_path/core/bus_center/ipc/small/src/bus_center_client_proxy.c",
      "$dsoftbus_root_path/core/bus_center/ipc/small/src/lnn_bus_center_ipc.c",
    ]
    bus_center_server_inc +=
        [ "$dsoftbus_root_path/core/bus_center/ipc/small/include" ]
    bus_center_server_deps += [
      "//build/lite/config/component/cJSON:cjson_shared",
      "//foundation/communication/ipc/interfaces/innerkits/c/ipc:ipc_single",
    ]
  }
} else {
  bus_center_server_src += [
    "$dsoftbus_root_path/core/bus_center/ipc/$os_type/src/bus_center_client_proxy.cpp",
    "$dsoftbus_root_path/core/bus_center/ipc/$os_type/src/bus_center_client_proxy_standard.cpp",
    "$dsoftbus_root_path/core/bus_center/ipc/$os_type/src/lnn_bus_center_ipc.cpp",
  ]
  bus_center_server_inc += [
    "$dsoftbus_root_path/core/bus_center/ipc/$os_type/include",
    "$dsoftbus_root_path/sdk/frame/$os_type/include",
    "//utils/system/safwk/native/include",
  ]
  bus_center_server_external_deps =
      bus_center_adapter_external_deps + bus_center_ledger_external_deps
  bus_center_server_external_deps += [ "c_utils:utils" ]
}

native_source_path = rebase_path("$dsoftbus_root_path")
agcr_dir = "dsoftbus_enhance/components/agcr"
agcr_enhanced = exec_script("$dsoftbus_root_path/check_sub_module.py",
                            [
                              "$native_source_path",
                              "$agcr_dir",
                            ],
                            "value")

if (agcr_enhanced) {
  import(
      "//foundation/communication/dsoftbus/dsoftbus_enhance/components/agcr/agcr.gni")

  bus_center_server_src += agcr_src
  bus_center_server_inc += agcr_inc
}
